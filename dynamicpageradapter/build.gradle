def libraryGroupId = 'dmt'
def libraryArtifactId = 'hephaestus'//replace with your library's name
def libraryVersion = '1.0.0'

buildscript {
    repositories {
        maven {
            jcenter()
            mavenCentral()
            url 'http://192.168.200.150:8081/artifactory/plugins-release'
            credentials {
                username = artifactory_username
                password = artifactory_password
            }
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }


}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    archivesBaseName = 'hephaestus'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {

                if (buildType.name == buildTypes.release.name) {
                    def fileName = "${archivesBaseName}-${libraryVersion}.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                    println("output file path ${output.outputFile}")
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${archivesBaseName}-${libraryVersion}.aar")
        }
    }
}

artifactory {
    contextUrl = artifactory_contextUrl
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = artifactory_username
            password = artifactory_password
            maven = true

        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
